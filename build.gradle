buildscript {
    ext.kotlin_version = '1.3.21'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.7'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.8.1"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
    }
}

subprojects {
    version = '0.0.3-SNAPSHOT'
    group = 'io.marauder.supercharger'

    plugins.withType(JavaPlugin) {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'

        plugins.withType(MavenPlugin) {
            javadoc {
                // link to JRE6 documentation
                options.author true
                options.links 'http://download.oracle.com/javase/8/docs/api/'
            }
            
            // package javadoc into a jar file
            task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
                from javadoc.destinationDir
                classifier = 'javadoc'
            }

            // package source into a jar file
            task packageSources(type: Jar) {
                from sourceSets.main.allSource
                classifier = 'sources'
            }

            // define artifacts for upload
            artifacts {
                archives jar
                archives packageJavadoc
                archives packageSources
            }
            
        }
    }

    ext {
        kotlinVersion = '3.11.0'
    }

    plugins.withType(MavenPlugin) {

        uploadArchives.repositories.mavenDeployer {
            // upload to geocode artifactory
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: rootProject.hasProperty('ossrhUsername') ? "$ossrhUsername" : "$System.env.ossrhUsername",
                    password: rootProject.hasProperty('ossrhPassword') ? "$ossrhPassword" : "$System.env.ossrhPassword")
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: this.hasProperty('ossrhUsername') ? "$ossrhUsername" : "$System.env.ossrhUsername",
                    password: this.hasProperty('ossrhPassword') ? "$ossrhPassword" : "$System.env.ossrhPassword")
            }

            // ensure correct artifact ID
            pom.artifactId = project.name

            // pom file details
            pom.project {
                name 'supercharger'
                packaging 'jar'
                description 'A Collection of Vector Tile Tools including the supercharged Encoder!'
                url 'http://marauder.io/supercharger'

                scm {
                    url 'scm:git:git://https://github.com/marauder-io/supercharger.git'
                    connection 'scm:git:git://https://github.com/marauder-io/supercharger.git'
                    developerConnection 'scm:git:git://https://github.com/marauder-io/supercharger.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'florian-zouhar'
                        name 'Florian Zouhar'
                        email 'florian.zouhar@igd.fraunhofer.de'
                    }
                }
            }
        }
    }

}
